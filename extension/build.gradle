buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.4'
    }
}

apply plugin: 'groovy'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

sourceCompatibility = 1.8
targetCompatibility = 1.8

group = "${releaseGroup}"
version = "${releaseVersion}"

repositories {
    jcenter()
}

dependencies {
    compile "org.codehaus.groovy:groovy-all:2.4.15"
    compile "org.scilab.forge:jlatexmath:1.0.7"
    compile "org.asciidoctor:asciidoctorj:1.5.8"

    testCompile 'org.spockframework:spock-core:1.1-groovy-2.4'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}

publishing {
    publications {
        'asciidoctor-mathx-extension'(MavenPublication) {
            groupId    "${releaseGroup}"
            artifactId "${releaseArtifact}"
            version    "${releaseVersion}"

            from components.java
            artifact sourcesJar
        }
    }
}

bintray {
    /* Normally taken from ~/.gradle/gradle.properties */
    user         = "${bintrayUser}"
    key          = "${bintrayKey}"
    publish      = true
    publications = ['asciidoctor-mathx-extension']
    pkg {
        repo            = 'maven'
        name            = "${releaseArtifact}"
        userOrg         = 'grooviter'
        desc            =  "${releaseDesc}"
        websiteUrl      = 'https://github.com/grooviter/asciidoctor-mathx-extension'
        issueTrackerUrl = 'https://github.com/grooviter/asciidoctor-mathx-extension/issues'
        licenses        = ['Apache-2.0']
        vcsUrl          = 'https://github.com/grooviter/asciidoctor-mathx-extension'
        labels          = ['asciidoctor', 'extension', 'groovy']
        version {
            name      = "${releaseVersion}"
            desc      = 'Asciidoctor LateX math extension release'
            released  = new Date()
            vcsTag    = "${releaseVersion}"
        }
    }
}